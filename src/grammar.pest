integer = @{ ("+"|"-"|"")? ~ ASCII_DIGIT+ }
floating = @{ ("+"|"-"|"")? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT*)? }

quantities = { integer }
tick_heights = { floating }

range = { range_start ~ (".." ~ range_end)? }
range_start = { integer }
range_end = { integer }

spec_tuple = { "(" ~ quantities ~ "," ~ tick_heights ~ ")" }
spec_follow = { split_all | split_subset }
split_all = { ">" ~ spec }
split_subset = { "{" ~ ranged_spec ~ ("," ~ ranged_spec)* ~ ","? ~ "}" }
ranged_spec = { range ~ ":" ~ spec }
spec = { spec_tuple ~ spec_follow? }

top_level = { SOI ~ spec ~ EOI }
WHITESPACE = _{ " " | "\n" }
COMMENT = _{ ("/*" ~ (!"*/" ~ ANY)* ~ "*/") | ("//" ~ (!"\n" ~ ANY)* ~ "\n") }
